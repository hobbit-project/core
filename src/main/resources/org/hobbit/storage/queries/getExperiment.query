PREFIX hobbit: <http://w3id.org/hobbit/vocab#>
PREFIX qb: <http://purl.org/linked-data/cube#>

CONSTRUCT {
    %EXPERIMENT_URI% a hobbit:Experiment .
    %EXPERIMENT_URI% hobbit:involvesSystemInstance ?system .
    %EXPERIMENT_URI% hobbit:involvesBenchmark ?benchmark .
    %EXPERIMENT_URI% ?experimentProp	?experimentObj .
    %EXPERIMENT_URI% hobbit:isPartOf ?challengeTask .
    ?challengeTask ?challengeTaskProp ?challengeTaskObject .
    %EXPERIMENT_URI% ?resultProp ?resultValue .
    ?resultProp a hobbit:KPI .
    ?resultProp ?resultPropProp ?resultPropObject .
    %EXPERIMENT_URI% ?parameterProp ?parameterValue .
    ?parameterProp a ?parameterType .
    ?parameterProp ?parameterPropProp ?parameterPropObject .
    ?benchmark ?benchmarkProp ?benchmarkObject .
    ?system ?systemProp ?systemObject .
    ?cubeDataSet ?cubeDataSetProp ?cubeDataSetValue .
    ?cubeDataSetStructure ?cubeDataSetStructureProp ?cubeDataSetStructureValue .
    ?cubeDataSetStructure qb:component ?cubeDataSetComponent .
    ?cubeDataSetComponent qb:dimension ?cubeDataSetDimension .
    ?cubeDataSetDimension a qb:DimensionProperty .
    ?cubeDataSetDimension ?cubeDataSetDimensionProp ?cubeDataSetDimensionValue .
    ?cubeDataSetComponent qb:measure ?cubeDataSetMeasure .
    ?cubeDataSetMeasure a qb:MeasureProperty .
    ?cubeDataSetMeasure ?cubeDataSetMeasureProperty ?cubeDataSetMeasureValue .
    ?observation ?observationProp ?observationValue .
}
WHERE {
    {
        GRAPH %GRAPH_URI% {
            %EXPERIMENT_URI% a hobbit:Experiment .
  	        %EXPERIMENT_URI% ?experimentProp ?experimentObj .
  	    }
    }
    UNION
    {
        GRAPH %GRAPH_URI% {
            %EXPERIMENT_URI% a hobbit:Experiment .
  	        %EXPERIMENT_URI% hobbit:involvesSystemInstance ?system .
  	        OPTIONAL { ?system ?systemProp ?systemObject . }
  	    }
    }
    UNION
    {
        GRAPH %GRAPH_URI% {
            %EXPERIMENT_URI% a hobbit:Experiment .
  	        %EXPERIMENT_URI% hobbit:involvesBenchmark ?benchmark .
  	        OPTIONAL { ?benchmark ?benchmarkProp ?benchmarkObject . }
  	    }
    }
    UNION
    {
        GRAPH %GRAPH_URI% {
            %EXPERIMENT_URI% a hobbit:Experiment .
            %EXPERIMENT_URI% hobbit:isPartOf ?challengeTask .
            ?challengeTask ?challengeTaskProp ?challengeTaskObject .
  	    }
    }
    UNION
    {
        GRAPH %GRAPH_URI% {
            %EXPERIMENT_URI% a hobbit:Experiment .
            %EXPERIMENT_URI% ?resultProp ?resultValue .
  	        %EXPERIMENT_URI% hobbit:involvesBenchmark ?benchmark .
            ?benchmark hobbit:measuresKPI ?resultProp .
            ?resultProp a hobbit:KPI .
            OPTIONAL { ?resultProp ?resultPropProp ?resultPropObject . }
  	    }
    }
    UNION
    {
        GRAPH %GRAPH_URI% {
            %EXPERIMENT_URI% a hobbit:Experiment .
  	        %EXPERIMENT_URI% hobbit:involvesBenchmark ?benchmark .
            ?benchmark hobbit:hasParameter ?parameterProp .
            %EXPERIMENT_URI% ?parameterProp ?parameterValue .
            ?parameterProp a ?parameterType .
            {?parameterProp a hobbit:Parameter} UNION {?parameterProp a hobbit:ConfigurableParameter} UNION {?parameterProp a hobbit:FeatureParameter}.
            OPTIONAL { ?parameterProp ?parameterPropProp ?parameterPropObject . }
        }
    }
    UNION
    {
        GRAPH %GRAPH_URI% {
            %EXPERIMENT_URI% a hobbit:Experiment .
            %EXPERIMENT_URI% ?resultProp ?cubeDataSet .
        ?cubeDataSet a qb:DataSet .
        ?cubeDataSet ?cubeDataSetProp ?cubeDataSetValue .
            OPTIONAL { 
                ?observation qb:DataSet ?cubeDataSet . 
                ?observation a qb:Observation .
                ?observation ?observationProp ?observationValue .
                }
        }
    }
    UNION
    {
        GRAPH %GRAPH_URI% {
            %EXPERIMENT_URI% a hobbit:Experiment .
            %EXPERIMENT_URI% ?resultProp ?cubeDataSet .
        ?cubeDataSet a qb:DataSet .
        ?cubeDataSet ?cubeDataSetProp ?cubeDataSetValue .
        ?cubeDataSet qb:structure ?cubeDataSetStructure .
            ?cubeDataSetStructure a qb:DataStructureDefinition .
            ?cubeDataSetStructure ?cubeDataSetStructureProp ?cubeDataSetStructureValue .
            ?cubeDataSetStructure qb:component ?cubeDataSetComponent .
            OPTIONAL { 
                ?cubeDataSetComponent qb:dimension ?cubeDataSetDimension .
                ?cubeDataSetDimension a qb:DimensionProperty .
                ?cubeDataSetDimension ?cubeDataSetDimensionProp ?cubeDataSetDimensionValue .
                }
            OPTIONAL { 
                ?cubeDataSetComponent qb:measure ?cubeDataSetMeasure .
                ?cubeDataSetMeasure a qb:MeasureProperty .
                ?cubeDataSetMeasure ?cubeDataSetMeasureProperty ?cubeDataSetMeasureValue .
                }
        }
    }
}
